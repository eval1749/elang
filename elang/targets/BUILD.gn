# Copyright 2014-2015 Project Vogue. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//elang/build/elang_target_arch.gni")
import("//testing/test.gni")

static_library("targets") {
  output_name = "elang_targets"
  sources = [
    "bytes.cc",
    "bytes.h",
    "target_features.h",
  ]

  public_deps = [
    "//base",
    "//elang:elang_target_arch",
  ]

  if (elang_target_arch == "x64") {
    sources += [
      "instructions_x64.h",
      "operand_x64.cc",
      "operand_x64.h",
      "register_x64.cc",
      "register_x64.h",
    ]
  }
}

source_set("test_files") {
  visibility = [ ":*" ]
  testonly = true
  sources = [
    "bytes_test.cc",
  ]

  public_deps = [
    "//testing/gtest",
    ":targets",
  ]

  if (elang_target_arch == "x64") {
    sources += [
      "operand_x64_test.cc",
      "register_x64_test.cc",
    ]
  }
}

test("tests") {
  output_name = "elang_targets_tests"
  deps = [
    "//base/test:run_all_unittests",
    ":test_files",
  ]
}
