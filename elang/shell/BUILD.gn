# Copyright 2014-2015 Project Vogue. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//testing/test.gni")

executable("shell") {
  output_name = "elang_shell"
  sources = [
    "compiler.cc",
    "compiler.h",
    "shell_main.cc",
  ]

  deps = [
    ":shell_library",
    "//elang/api",
    "//elang/cg",
    "//elang/optimizer",
    "//elang/translator",
  ]
}

source_set("shell_library") {
  visibility = [ ":*" ]
  sources = [
    "namespace_builder.cc",
    "namespace_builder.h",
    "node_query.cc",
    "node_query.h",
    "source_file_stream.cc",
    "source_file_stream.h",
    "utf8_decoder.cc",
    "utf8_decoder.h",
  ]
  public_deps = [
    "//base",
    "//elang/base",
    "//elang/compiler",
    "//elang/vm",
  ]
}

source_set("test_files") {
  visibility = [ ":*" ]
  testonly = true
  sources = [
    "utf8_decoder_unittest.cc",
  ]
  public_deps = [
    ":shell_library",
    "//testing/gtest",
  ]
}

test("tests") {
  output_name = "elang_shell_tests"
  deps = [
    ":test_files",
    "//base/test:run_all_unittests",
  ]
}
