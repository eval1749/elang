# Copyright 2014-2015 Project Vogue. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//elang/build/elang_target_arch.gni")
import("//testing/test.gni")

executable("shell") {
  output_name = "elang_shell"
  sources = [
    "compiler.cc",
    "compiler.h",
    "disasm.h",
    "shell_main.cc",
  ]

  deps = [
    ":shell_library",
    "//elang/api",
    "//elang/cg",
    "//elang/optimizer",
    "//elang/targets",
    "//elang/translator",
  ]

  if (elang_target_arch == "x64") {
    sources += [ "disasm_x64.cc" ]
  }
}

source_set("shell_library") {
  visibility = [ ":*" ]
  sources = [
    "node_query.cc",
    "node_query.h",
    "pass_record.cc",
    "pass_record.h",
    "source_file_stream.cc",
    "source_file_stream.h",
    "utf8_decoder.cc",
    "utf8_decoder.h",
  ]
  public_deps = [
    "//base",
    "//elang/base",
    "//elang/compiler",
    "//elang/vm",
  ]
}

source_set("test_files") {
  visibility = [ ":*" ]
  testonly = true
  sources = [
    "utf8_decoder_unittest.cc",
  ]
  public_deps = [
    ":shell_library",
    "//testing/gtest",
  ]
}

test("tests") {
  output_name = "elang_shell_tests"
  deps = [
    ":test_files",
    "//base/test:run_all_unittests",
  ]
}
