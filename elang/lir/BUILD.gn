# Copyright 2014-2015 Project Vogue. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//elang/build/elang_target_arch.gni")

component("lir") {
  output_name = "elang_lir"
  sources = [
    "code_emitter.cc",
    "code_emitter.h",
    "editor.cc",
    "editor.h",
    "factory.cc",
    "factory.h",
    "formatters/text_formatter.cc",
    "formatters/text_formatter.h",
    "instructions.cc",
    "instructions.h",
    "instruction_visitor.cc",
    "instruction_visitor.h",
    "isa.h",
    "literals.cc",
    "literals.h",
    "literals_forward.h",
    "literal_map.cc",
    "literal_map.h",
    "literal_visitor.cc",
    "literal_visitor.h",
    "printable.h",
    "value.h",
    "value.cc",
  ]

  defines = [ "LIR_IMPLEMENTATION" ]

  public_deps = [
    "//base",
    "//elang/api",
    "//elang/base",
    "//elang:elang_target_arch",
  ]

  if (elang_target_arch == "x64") {
    sources += [
      "code_emitter_x64.cc",
      "instructions_x64.cc",
      "isa_x64.h",
      "isa_x64.cc",
      "opcodes_x64.h",
    ]
  }
}

source_set("test_support") {
  testonly = true
  sources = [
    "testing/lir_test.cc",
    "testing/lir_test.h",
    "testing/test_machine_code_builder.cc",
  ]
  public_deps = [
    "//testing/gtest",
    "//elang/api",
    ":lir",
  ]
  if (elang_target_arch == "x64") {
    sources += [
      "testing/lir_test_x64.cc",
      "testing/lir_test_x64.h",
    ]
  }
}

source_set("lir_unittests") {
  testonly = true
  sources = [
    "instructions_unittest.cc",
    "literals_unittest.cc",
    "value_unittest.cc",
  ]
  public_deps = [
    ":test_support",
  ]
  if (elang_target_arch == "x64") {
    sources += [
      "code_emitter_x64_unittest.cc",
      "instructions_x64_unittest.cc",
    ]
  }
}

test("elang_lir_unittests") {
  deps = [
    "//base/test:run_all_unittests",
    ":lir_unittests",
  ]
}
