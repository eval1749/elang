# Copyright 2014-2015 Project Vogue. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//elang/build/elang_target_arch.gni")
import("//testing/test.gni")

static_library("vm") {
  output_name = "elang_vm"

  sources = [
    "class.cc",
    "class.h",
    "collectable.cc",
    "collectable.h",
    "entry_point.h",
    "factory.cc",
    "factory.h",
    "machine_code_annotation.h",
    "machine_code_builder_impl.cc",
    "machine_code_builder_impl.h",
    "machine_code_collection.cc",
    "machine_code_collection.h",
    "machine_code_function.cc",
    "machine_code_function.h",
    "memory_pool.cc",
    "memory_pool.h",
    "namespace.cc",
    "namespace.h",
    "namespace_member.cc",
    "namespace_member.h",
    "node.cc",
    "node.h",
    "object_factory.cc",
    "object_factory.h",
    "objects.cc",
    "objects.h",
    "virtual_memory.h",
  ]

  public_deps = [
    "//base",
    "//elang/base",
    "//elang/targets",
  ]

  if (is_win) {
    sources += [
      "platform/virtual_memory_win.cc",
      "platform/virtual_memory_win.h",
    ]
  }
}

source_set("test_support") {
  visibility = [ ":*" ]
  testonly = true
  public_deps = [
    "//testing/gtest",
    "//elang/api",
    ":vm",
  ]
}

source_set("vm_tests") {
  visibility = [ ":*" ]
  testonly = true
  sources = [
    "machine_code_builder_impl_unittest.cc",
    "namespace_unittest.cc",
  ]
  public_deps = [
    ":test_support",
  ]
}

test("tests") {
  output_name = "elang_vm_tests"
  visibility = [ "//elang:tests" ]
  sources = []

  deps = [
    "//base/test:run_all_unittests",
    ":vm_tests",
  ]
}
