# Copyright 2014-2015 Project Vogue. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//testing/test.gni")

static_library("compiler") {
  sources = [
    "character_stream.cc",
    "character_stream.h",
    "compilation_session.cc",
    "compilation_session.h",
    "compilation_session_user.cc",
    "compilation_session_user.h",
    "compilation_unit.cc",
    "compilation_unit.h",
    "compile.cc",
    "modifiers.cc",
    "modifiers.h",
    "modifiers_builder.cc",
    "modifiers_builder.h",
    "parameter_kind.cc",
    "parameter_kind.h",
    "predefined_names.cc",
    "predefined_names.h",
    "public/compiler_error_code.h",
    "public/compiler_error_data.cc",
    "public/compiler_error_data.h",
    "source_code.cc",
    "source_code.h",
    "source_code_position.cc",
    "source_code_position.h",
    "source_code_range.cc",
    "source_code_range.h",
    "string_source_code.cc",
    "string_source_code.h",
    "string_stream.cc",
    "string_stream.h",
    "syntax/lexer.cc",
    "syntax/lexer.h",
    "syntax/parse_expression.cc",
    "syntax/parse_statement.cc",
    "syntax/parse_type.cc",
    "syntax/parser.cc",
    "syntax/parser.h",
    "token.cc",
    "token.h",
    "token_data.cc",
    "token_data.h",
    "token_factory.cc",
    "token_factory.h",
    "token_type.cc",
    "token_type.h",
  ]

  deps = [
    "//base",
    "//elang/base",
    "//elang/compiler/analysis",
    "//elang/compiler/ast",
    "//elang/compiler/cg",
    "//elang/compiler/semantics",
    "//elang/compiler/translate",
  ]
}

source_set("test_support") {
  visibility = [
    ":*",
    "./*",
  ]

  testonly = true
  sources = [
    "testing/analyzer_test.cc",
    "testing/analyzer_test.h",
    "testing/compiler_test.cc",
    "testing/compiler_test.h",
    "testing/formatter.cc",
    "testing/formatter.h",
    "testing/namespace_builder.cc",
    "testing/namespace_builder.h",
  ]
  public_deps = [
    ":compiler",
    "//testing/gtest",
  ]
}

source_set("test_files") {
  visibility = [ ":*" ]
  testonly = true
  sources = [
    "syntax/lexer_test.cc",
    "syntax/parser_test.cc",
    "token_test.cc",
  ]
  public_deps = [
    ":test_support",
  ]
  deps = [
    "//elang/compiler/analysis:compiler_analysis_test",
    "//elang/compiler/cg:compiler_cg_test",
    "//elang/compiler/semantics:test_files",
    "//elang/compiler/translate:test_files",
  ]
}

test("tests") {
  output_name = "elang_compiler_tests"
  deps = [
    ":test_files",
    "//base/test:run_all_unittests",
  ]
}
